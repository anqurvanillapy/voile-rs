//
// Created by intellij-pest on 2019-04-06
// voile grammar
// Author: ice1000
//

WHITESPACE = _{ WHITE_SPACE | ("//" ~ (!NEWLINE ~ ANY)*) }

/// Identifier
ident_char =
 _{ ASCII_ALPHA
  | "_"
  | "\\"
  }
ident_raw =
 _{ ident_char
  ~ ( ident_char
    | ASCII_DIGIT
    | "'"
    )*
  }
ident = @{ ident_raw }
cons = @{ "'" ~ ident_raw }
meta = @{ "_" }

// TODO: this syntax is intended to be a placeholder and going to be redesigned
// TODO: since even its designer consider it ugly
cons_type = @{ "," ~ ident_raw }

/// Keywords
val_keyword = _{ "val" }
let_keyword = _{ "let" }
type_level = @{ ASCII_DIGIT* }
type_keyword = ${ "Type" ~ type_level }
semicolon = _{ ";" }

expr = { dollar_expr }

dollar_expr = { pipe_expr ~ ("$" ~ pipe_expr)* }
pipe_expr = { sum_expr ~ ("|>" ~ sum_expr)* }
sum_expr = { app_expr ~ ("|" ~ app_expr)* }
app_expr = { primary_expr+ }
primary_expr =
 { type_keyword
 | cons
 | meta
 | ident
 | "(" ~ expr ~ ")"
 }

/// ML-style type signature and definition
signature = { val_keyword ~ ident ~ ":" ~ expr ~ semicolon }
implementation = { let_keyword ~ ident ~ "=" ~ expr ~ semicolon }

declaration =
 { signature
 | implementation
 }

// File
file = { WHITESPACE* ~ declaration* }
